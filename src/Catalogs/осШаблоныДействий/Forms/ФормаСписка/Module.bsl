
&НаСервере
Функция ОбновитьШаблоныНаСервере()
	
	ТаблицаПредопределенныхШаблонов = осОчередиСообщенийСервер.ПолучитьТаблицуПредопределенныхШаблоновДействий();
	
	МассивОбновленныхКодов = Новый Массив;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаПредопределенныхШаблонов.Код КАК Код,
	               |	ТаблицаПредопределенныхШаблонов.Наименование КАК Наименование,
	               |	ТаблицаПредопределенныхШаблонов.Тип КАК Тип,
	               |	ТаблицаПредопределенныхШаблонов.Метод КАК Метод,
	               |	ТаблицаПредопределенныхШаблонов.ПараметрыМетода КАК ПараметрыМетода,
	               |	ТаблицаПредопределенныхШаблонов.Хеш КАК Хеш
	               |ПОМЕСТИТЬ ВТ_ПредопределенныеМетоды
	               |ИЗ
	               |	&ТаблицаПредопределенныхШаблонов КАК ТаблицаПредопределенныхШаблонов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ПредопределенныеМетоды.Код КАК Код,
	               |	ВТ_ПредопределенныеМетоды.Наименование КАК Наименование,
	               |	ВТ_ПредопределенныеМетоды.Тип КАК Тип,
	               |	ВТ_ПредопределенныеМетоды.Метод КАК Метод,
	               |	ВТ_ПредопределенныеМетоды.ПараметрыМетода КАК ПараметрыМетода,
	               |	ВТ_ПредопределенныеМетоды.Хеш КАК Хеш,
	               |	осШаблоныДействий.Ссылка КАК Ссылка
	               |ИЗ
	               |	ВТ_ПредопределенныеМетоды КАК ВТ_ПредопределенныеМетоды
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.осШаблоныДействий КАК осШаблоныДействий
	               |		ПО (осШаблоныДействий.Код = ВТ_ПредопределенныеМетоды.Код)
	               |			И (осШаблоныДействий.Хеш <> ВТ_ПредопределенныеМетоды.Хеш)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	осШаблоныДействий.Код,
	               |	осШаблоныДействий.Наименование,
	               |	ЗНАЧЕНИЕ(Перечисление.осТипыДействий.Пользовательский),
	               |	осШаблоныДействий.Метод,
	               |	осШаблоныДействий.ПараметрыМетода,
	               |	"""",
	               |	осШаблоныДействий.Ссылка
	               |ИЗ
	               |	Справочник.осШаблоныДействий КАК осШаблоныДействий
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредопределенныеМетоды КАК ВТ_ПредопределенныеМетоды
	               |		ПО осШаблоныДействий.Код = ВТ_ПредопределенныеМетоды.Код
	               |ГДЕ
	               |	ВТ_ПредопределенныеМетоды.Код ЕСТЬ NULL
	               |	И осШаблоныДействий.Тип = ЗНАЧЕНИЕ(Перечисление.осТипыДействий.Предопределенный)";
	Запрос.УстановитьПараметр("ТаблицаПредопределенныхШаблонов",ТаблицаПредопределенныхШаблонов);
	                                 
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.осШаблоныДействий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	БлокировкаДанных.Заблокировать();
	
	Выборка = Запрос.Выполнить().Выбрать();
	 
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленныхКодов.Добавить(Выборка.Код);
		
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			Объект = Выборка.Ссылка.ПолучитьОбъект();
		Иначе
			Объект = Справочники.осШаблоныДействий.СоздатьЭлемент();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, Выборка);
		Объект.Записать();
				
	КонецЦикла;	
			
	ЗафиксироватьТранзакцию();
			
	Возврат МассивОбновленныхКодов;
	 	
КонецФункции

&НаКлиенте
Процедура ОбновитьШаблоны(Команда)
	
	МассивОбновленныхКодов = ОбновитьШаблоныНаСервере();
	Если МассивОбновленныхКодов.Количество() Тогда
		Сообщить("Обновлены следующие элементы:");
		Для Каждого Элемент Из МассивОбновленныхКодов Цикл
			Сообщить(Элемент);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОбъектШаблонаИзОписанияШаблона(Код, ОписаниеШаблона);
	
	ШаблонОбъект = Справочники.осШаблоныДействий.СоздатьЭлемент();
	ШаблонОбъект.Код = Код;
	ШаблонОбъект.Наименование = ОписаниеШаблона.Наименование;
	ШаблонОбъект.Метод = ОписаниеШаблона.Метод;
	ШаблонОбъект.Тип = ПредопределенноеЗначение("Перечисление.осТипыДействий.Предопределенный");
	ШаблонОбъект.ОписаниеПараметров = Новый ХранилищеЗначения(ОписаниеШаблона.Параметры);
	
	Возврат ШаблонОбъект;
	
КонецФункции

